package com.sbz;

import com.sbz.proj.model.*;

global java.util.logging.Logger LOGGER

rule "All in when you have royal flush"
    lock-on-active true
    agenda-group "river"
	when
        $state: TableState(
            $players: players,
            $player: players.get(0),
            $player.getAction().get(0) == Action.NONE
        )
        $possibility: Possibility(
            $pokerHand: pokerHand == PokerHand.ROYAL_FLUSH
        )
    then
        modify($state){
            getPlayers().get(0).getAction().set(0, Action.ALL_IN);
        }
end

rule "All in when you have a good combination"
    lock-on-active true
    agenda-group "river"
	when
        $state: TableState(
            $players: players,
            $player: players.get(0),
            $player.getAction().get(0) == Action.NONE
        )
        $possibility: Possibility(
            pokerHand != PokerHand.ROYAL_FLUSH,
            $pokerHand: pokerHand,
            pokerHand.strength >= 4
        )
    then
        modify($state){
            getPlayers().get(0).getAction().set(0, Action.ALL_IN);
        }
        LOGGER.info("You have " + $pokerHand.name());
end

rule "Call in river when you have 2 pairs and opponents haven't raised more than 1.5 times your pot"
    lock-on-active true
    agenda-group "river"
	when
        $state: TableState(
            $players: players,
            $player: players.get(0),
            $player.getAction().get(0) == Action.NONE
        )
        $possibility: Possibility(
            $pokerHand: pokerHand,
            $pokerHand == PokerHand.TWO_PAIRS
        )
        Player(getAction().get(0) == Action.RAISE || getAction().get(0) == Action.BET) from $players
        accumulate( Player($pot : getMoney()) from $players, $maxMoney : max($pot))
        Player(moneyRatio($maxMoney.doubleValue()) <= 1.5) from $player
    then
        modify($state){
            getPlayers().get(0).getAction().set(0, Action.CALL);
        }
        LOGGER.info("You have " + $pokerHand.name());
        LOGGER.info("You should call to "+ $maxMoney);
end

rule "Call in river when you have only strong pocket pair"
    lock-on-active true
    agenda-group "river"
	when
        $state: TableState(
            $players: players,
            $player: players.get(0),
            $player.getAction().get(0) == Action.NONE,
            $player.getCard1().rank.rank == $player.getCard2().rank.rank,
            $player.getCard1().rank.rank + $player.getCard2().rank.rank >= 20
        )
        $possibility: Possibility(
            $pokerHand: pokerHand,
            pokerHand == PokerHand.PAIR
        )
        Player(getAction().get(0) == Action.RAISE || getAction().get(0) == Action.BET) from $players
        accumulate( Player($pot : getMoney()) from $players, $maxMoney : max($pot))
        Player(moneyRatio($maxMoney.doubleValue()) <= 1.3) from $player
    then
        modify($state){
            getPlayers().get(0).getAction().set(0, Action.CALL);
        }
        LOGGER.info("You have " + $pokerHand.name());
        LOGGER.info("You should call to "+ $maxMoney);
end

rule "Fold in river when you have weak pair and someone raised over 1.3 times your own pot"
    lock-on-active true
    agenda-group "river"
	when
        $state: TableState(
            $players: players,
            $player: players.get(0),
            $player.getAction().get(0) == Action.NONE,
            $player.getCard1().rank.rank == $player.getCard2().rank.rank,
			$player.getCard1().rank.rank < 10
        )
        $possibility: Possibility(
            $pokerHand: pokerHand,
            pokerHand == PokerHand.PAIR
        )
        Player(getAction().get(0) == Action.RAISE || getAction().get(0) == Action.BET) from $players
        accumulate( Player($pot : getMoney()) from $players, $maxMoney : max($pot))
        Player(moneyRatio($maxMoney.doubleValue()) > 1.3) from $player
    then
        modify($state){
            getPlayers().get(0).getAction().set(0, Action.FOLD);
        }
        LOGGER.info("You have " + $pokerHand.name());
        LOGGER.info(", a weak one, you should fold.");
end

rule "Fold in river when you have nothing and someone raised"
    lock-on-active true
    agenda-group "river"
	when
        $state: TableState(
            $players: players,
            $player: players.get(0),
            players.get(0).getCard1().rank.rank >= 12 || players.get(0).getCard2().rank.rank >= 12,
            $player.getAction().get(0) == Action.NONE
        )
        $possibility: Possibility(
            $pokerHand: pokerHand,
            pokerHand.strength <= 1
        )
        Player(getAction().get(0) == Action.RAISE || getAction().get(0) == Action.BET) from $players
    then
        modify($state){
            getPlayers().get(0).getAction().set(0, Action.FOLD);
        }
        LOGGER.info("You have " + $pokerHand.name());
        LOGGER.info("You should fold.");
end

rule "Fold in river when you have nothing"
    agenda-group "river"
	when
        $state: TableState(
            $players: players,
            $player: players.get(0),
            $player.getAction().get(0) == Action.NONE
        )
        $possibility: Possibility(
            $pokerHand: pokerHand,
			$pokerHand == PokerHand.HIGH_CARD || $pokerHand == PokerHand.UNKNOWN
        )
    then
        modify($state){
            getPlayers().get(0).getAction().set(0, Action.FOLD);
        }
        LOGGER.info("You have " + $pokerHand.name());
end

rule "No one in river raised, check with an ok hand"
	agenda-group "river"
	when
		$state : TableState(
			$players: players,
            $player: players.get(0),
            $player.getAction().get(0) == Action.NONE
		)
		$possibility: Possibility(
            $pokerHand: pokerHand,
			$pokerHand.strength <= 4
        )
		not(Player(getAction().get(0) == Action.RAISE || getAction().get(0) == Action.BET) from $players) 
	then
		modify($state){
            getPlayers().get(0).getAction().set(0, Action.CHECK);
        }
        LOGGER.info("You have " + $pokerHand.name()+ " on river, you should check");
end


rule "No one in river raised, raise with a good hand"
	agenda-group "river"
	when
		$state : TableState(
			$players: players,
            $player: players.get(0),
            $player.getAction().get(0) == Action.NONE
		)
		$possibility: Possibility(
            $pokerHand: pokerHand,
			$pokerHand.strength > 4
        )
		not(Player(getAction().get(0) == Action.RAISE || getAction().get(0) == Action.BET) from $players) 
	then
		modify($state){
            getPlayers().get(0).getAction().set(0, Action.RAISE);
        }
        LOGGER.info("You have " + $pokerHand.name()+ " on river, you should raise");
end
